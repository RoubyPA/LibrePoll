;;; guix --- LibrePoll guix package.                   -*- mode: scheme -*-
;;; Copyright (C) 2019  Pierre-Antoine Rouby

(use-modules ((guix licenses) #:prefix l:)
             (guix packages)
             (guix download)
             (guix gexp)
             (guix utils)
             (guix build-system trivial)
             (gnu packages)
             (gnu packages bash)
             (gnu packages guile)
             (gnu packages guile-xyz))
(package
  (name "librepoll")
  (version "v0.0.0")
  (source (local-file "." "librepoll" #:recursive? #t))
  (build-system trivial-build-system)
  (inputs
   `(("bash", bash-minimal)
     ("guile" ,guile-2.2)
     ("guile-json" ,guile-json)
     ("guile-sqlite3" ,guile-sqlite3)
     ("guile-commonmark" ,guile-commonmark)))
  (arguments
   `(#:modules ((guix build utils))
     #:builder
     (begin
       (use-modules (guix build utils)
                    (srfi srfi-26)
                    (ice-9 popen)
                    (ice-9 rdelim))

       (let* ((out (assoc-ref %outputs "out"))
              (bin (string-append out "/bin"))
              (shr (string-append out "/share/librepoll"))
              (bash (string-append (assoc-ref %build-inputs "bash") "/bin"))
              (src (assoc-ref %build-inputs "source"))
              (guile (assoc-ref %build-inputs "guile"))
              (deps (map (lambda (s) (assoc-ref %build-inputs s))
                         (list "guile" "guile-commonmark"
                               "guile-sqlite3" "guile-json")))
              (effective
               (read-line
                (open-pipe* OPEN_READ
                            (string-append guile "/bin/guile")
                            "-c" "(display (effective-version))")))
              (path (string-join
                     (map (cut string-append <>
                               "/share/guile/site/"
                               effective)
                          deps)
                     ":"))
              (gopath (string-join
                       (map (cut string-append <>
                                 "/lib/guile/" effective
                                 "/site-ccache")
                            deps)
                       ":")))
         ;; Set environ var
         (setenv "PATH" (string-append bash ":" (getenv "PATH")))

         ;; Copy source
         (copy-recursively src ".")

         ;; Subsitute librepoll
         (substitute* "librepoll"
           (("#!/bin/guile")
            (string-append "#!" guile "/bin/guile"))
           (("\\(define share-path \"\"\\)")
            (string-append "(define share-path \"" shr "/\")")))

         ;; Create out dir
         (mkdir-p bin)
         (mkdir-p shr)

         ;; Install and wrap prog
         (copy-file "librepoll" (string-append bin "/librepoll"))
         (wrap-program (string-append bin "/librepoll")
           `("GUILE_LOAD_PATH" ":" prefix (,path))
           `("GUILE_LOAD_COMPILED_PATH" ":" prefix (,gopath)))

         ;; Copy additional file
         (copy-recursively (string-append src "/assets/")
                           (string-append shr "/assets/"))
         (copy-file (string-append src "/librepoll.json")
                    (string-append shr "/librepoll.json"))
         (copy-file (string-append src "/LICENSE")
                    (string-append shr "/LICENSE"))
         #t))))
  (synopsis "Free poll web interface")
  (description
   "LibrePoll provide a web interface to create opinion poll.")
  (home-page "https://framagit.org/prouby/librepoll")
  (license l:agpl3+))
