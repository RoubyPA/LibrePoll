;;; guix --- LibrePoll guix package.                   -*- mode: scheme -*-
;;; Copyright (C) 2019  Pierre-Antoine Rouby

(use-modules ((guix licenses) #:prefix l:)
             (guix packages)
             (guix download)
             (guix gexp)
             (guix utils)
             (guix build-system gnu)
             (gnu packages)
             (gnu packages autotools)
             (gnu packages pkg-config)
             (gnu packages guile)
             (gnu packages guile-xyz))
(package
  (name "librepoll")
  (version "v0.0.0")
  (source (local-file "." "librepoll" #:recursive? #t))
  (build-system gnu-build-system)
  (native-inputs
   `(("autoconf" ,autoconf)
     ("automake" ,automake)
     ("pkg-config" ,pkg-config)))
  (inputs
   `(("guile" ,guile-2.2)
     ("guile-json" ,guile-json)
     ("guile-sqlite3" ,guile-sqlite3)
     ("guile-commonmark" ,guile-commonmark)))
  (arguments
   `(#:modules ((guix build gnu-build-system)
                (guix build utils)
                (srfi srfi-26)
                (ice-9 popen)
                (ice-9 rdelim))
     #:phases
     (modify-phases %standard-phases
       (add-after 'install 'wrap
         (lambda* (#:key inputs outputs #:allow-other-keys)
           (let* ((out (assoc-ref outputs "out"))
                  (bin (string-append out "/bin"))
                  (shr (string-append out "/share/librepoll/"))
                  (src (assoc-ref inputs "source"))
                  (guile (assoc-ref inputs "guile"))
                  (deps (map (lambda (s) (assoc-ref inputs s))
                             (list "guile" "guile-commonmark"
                                   "guile-sqlite3" "guile-json")))
                  (effective
                   (read-line
                    (open-pipe* OPEN_READ
                                (string-append guile "/bin/guile")
                                "-c" "(display (effective-version))")))
                  (path (string-join
                         (map (cut string-append <>
                                   "/share/guile/site/"
                                   effective)
                              deps)
                         ":"))
                  (gopath (string-join
                           (map (cut string-append <>
                                     "/lib/guile/" effective
                                     "/site-ccache")
                                deps)
                           ":")))
             ;; Wrap prog
             (wrap-program (string-append bin "/run-librepoll")
               `("GUILE_LOAD_PATH" ":" prefix (,path))
               `("GUILE_LOAD_COMPILED_PATH" ":" prefix (,gopath))
               `("LIBREPOLL_ASSETS_DIR" ":" prefix (,shr)))
             #t))))))
  (synopsis "Free poll web interface")
  (description
   "LibrePoll provide a web interface to create opinion poll.")
  (home-page "https://framagit.org/prouby/librepoll")
  (license l:agpl3+))
